buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.7.1"
group= "gigaherz.enderthing" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "enderthing-1.10.2"

minecraft {
    version = "1.10.2-12.18.2.2102"
    runDir = "run"

    mappings = "snapshot_20161010"

    replace "@VERSION@", project.version
    replaceIn "Enderthing.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':Commons:'))
    }
    relocate 'gigaherz.common', 'gigaherz.enderthing.shadow.common'
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "gigaherz.commons:Commons:1.10.2-0.2"

    deobfCompile "mezz.jei:jei_1.10.2:3.12.6.304:api"
    runtime "mezz.jei:jei_1.10.2:3.12.6.304"

    compile fileTree(dir: 'libs', include: '*.jar')
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
